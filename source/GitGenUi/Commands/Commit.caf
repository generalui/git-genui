import &StandardImport, &UserConfig, &CommitLib

colorizeValue = &colors.yellow
colorNotPresent = &colors.grey

presentValue = (value, noneValue = :none) ->
  if present value
    if (value is Array) and value[0] is String
      array v in value
        colorizeValue v
      .join ', '

    else if value is String
      colorizeValue value

    else
      formattedInspect value, color: true
  else
    colorNotPresent noneValue

noop = (a) -> a

##
  IN: state:
    status:     git status
    stories:    tracker stories
    members:    tracker members
    myAccount:  current user's tracker account

CommitNow = (state) ->
  &Git.commit state
  .then ({branch, commit, summary}) ->
    {staged} = normalizeGitStatus state.status
    log success: {}
      committed: staged
      branch
      commit
      summary: object v in summary with v | 0
    null

ActionMenu = (state) ->
  state extract
    status
    stories
    members
    myAccount

    message
    type
    story
    coauthors

  otherMembers = array member in members when member.id != myAccount.id
  {staged, unstaged, untracked} = nStatus = normalizeGitStatus status
  statusColors =
    staged: :green
    unstaged: :red
    untracked: :red
  statusSummary =
    compactFlatten array statusCat from :staged :unstaged :untracked when nStatus[statusCat]?.length > 0
      &colors[statusColors[statusCat]] "" #{nStatus[statusCat].length} #{statusCat}
    .join ', '

  &PromptFor.selectList
    prompt:  "" Select action:
    items: compactFlatten []
      action: EditGitStage,       value: "" 1. Edit staged files:      #{} statusSummary
      action: StoryMenu,          value: "" 2. Select story:           #{} if !myAccount then colorNotPresent("configure tracker") else presentValue(if story then stripAnsi tracker.formatStory story)
      action: SelectCommitType,   value: "" 3. Select type:            #{} presentValue type
      action: EditCommitMessage,  value: "" 4. Edit message:           #{} presentValue message
      action: SelectCoauthors,    value: "" 5. Change coauthors:       #{} if !myAccount then colorNotPresent("configure tracker") else if otherMembers.length == 0 then colorNotPresent("only you") else presentValue coauthors

      # key: :autoPush    value: "" auto-push on/off
      # key: :storyStatus value: "" update story status: started -> finished / (no change: finished)
      action: CommitNow,          value: "" 6. Commit now              #{} &colors.grey getGitCommitMessage state
      key: :abort                 value: "" 0. Abort / quit

  .then ({action}) ->
    if action?
      log {} action
      Promise.then -> action state
      .then (newState) ->
        newState extract? message, type, story, coauthors
        userConfig.saveCommitOptionsForProject {}
          message, type, coauthors, story
        ActionMenu newState if newState


description: "" interactive commit2
run: (options) ->
  Promise.deepAll merge
    userConfig.commitOptionsForProject
    {message, coauthors, type, storyId} = options
    {}
      &Git.status
      tracker.stories
      tracker.members
      tracker.myAccount

  .then (loadedOptions) ->
    loadedOptions extract story, stories
    if storyId = story?.id
      unless story = find story in stories when story.id == storyId
        log.warn merge {}
          message: "" Invalid story or storyId
          story
          storyId

      loadedOptions.story = story

    ActionMenu loadedOptions

