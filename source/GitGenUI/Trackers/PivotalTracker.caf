import &StandardImport, &Config, &ArtRestClient

baseUrl = :https://www.pivotaltracker.com/services/v5

class PivotalTracker extends BaseClass
  @classGetter commonRestOptions: ->
    normalizedHeaders: X-TrackerToken: config.accounts.pivotalTracker.token

  #####################################
    READ
  #####################################
  @getProjects: =>
    getJson :https://www.pivotaltracker.com/services/v5/projects @commonRestOptions

  @getStories: (projectId, options) ->
    options extract? states
    states ?= :started :rejected :planned :unstarted :unscheduled :finished :delivered
    getJson "#{baseUrl}/projects/#{projectId}/stories?limit=500&date_format=millis&filter=state:#{states.join ','}" @commonRestOptions
    .then (stories) -> stories.map @_normalizeStory

  @getMyAccount: =>
    getJson :https://www.pivotaltracker.com/services/v5/me?date_format=millis @commonRestOptions
    .then @_normalizeAccount

  @getMembers: (projectId) =>
    getJson "#{baseUrl}/projects/#{projectId}/memberships" @commonRestOptions
    .then (memberships) ->
      array {person} in memberships
        {id, name, email} = person

  @openInBrowser: (projectId) ->
    &open @getProjectUrl projectId

  @getProjectUrl: (projectId) -> "" https://www.pivotaltracker.com/n/projects/#{projectId}

  @getStoryUrl: (projectId, storyId) ->
    @getProjectUrl projectId
    + "" /stories/#{storyId}

  @authenticate: ({username, password}) ->
    getJson :https://www.pivotaltracker.com/services/v5/me merge
      headers:
        Authorization: "" Basic #{} &btoa "#{username}:#{password}"
    .then @_normalizeAccount

  #####################################
    UPDATE
  #####################################
  @updateStory: (projectId, storyId, updates) ->
    {state, estimate} = updates
    if state
      throw new Error "invalid state: #{state}" unless state == "finished" or state == "started"

    putJson
      "" #{baseUrl}/projects/#{projectId}/stories/#{storyId}

      merge {}
        estimate
        current_state: state

      @commonRestOptions

  @createComment: (projectId, storyId, text) ->
    postJson
      "" #{baseUrl}/projects/#{projectId}/stories/#{storyId}/comments
      {} text
      @commonRestOptions

  #####################################
    PRIVATE
  #####################################
  @_normalizeStory: (story) ->
    story extract
      id, requested_by_id, current_state, story_type, name, updated_at, created_at, url, owner_ids
      estimate
    {}
      id
      url
      name
      estimate
      requestedById: requested_by_id
      ownerIds: owner_ids
      state: current_state
      updatedAt: updated_at
      createdAt: created_at

  @_normalizeAccount: (account) ->
    merge
      object v, k from account with-key
        if k == :api_token
          :token
        else lowerCamelCase k
      tracker: :PivotalTracker
