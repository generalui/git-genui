import &StandardImport

class UserConfig extends &ConfigShared
  @singletonClass()

  @getProjectKey: getProjectKey = ({origin, remotes, projectFolder}) ->
    origin ? remotes?[0]?.refs?.fetch ? projectFolder

  @classGetter
    configBasename: -> :git-genui.user.config.json
    configFilePathPromise: -> Promise.then -> &path.join &os.homedir(), @configBasename

  @getter
    :projectKey
    accounts: -> @config.accounts ? {}
    commitOptions: -> @config.commitOptions ? {}
    commitOptionsForProject: (projectKey = @projectKey) ->
      @commitOptions[projectKey]

  @setter
    accounts: (a) -> @setConfigProperty :accounts, a

  init: ->
    super
    .then -> Promise.deepAll {} &git.origin, &git.remotes, projectFolder: &ProjectConfig.repoRootPromise.then &path.basename
    .catch ->
    .then getProjectKey
    .then (k) -> @_projectKey = k

  saveCommitOptionsForProject: (commitOptions, projectKey = @projectKey) ->
    throw new Error "projectKey required to save default options" unless present projectKey
    @setConfigProperty
      :commitOptions
      merge @commitOptions,
        [projectKey]: merge
          commitOptions
          {}
            updatedAt = toSeconds()
            createdAt: @commitOptions[projectKey]?.createdAt ? updatedAt
