import &StandardImport

class UserConfig extends &ConfigShared
  @singletonClass()

  @classGetter
    configBasename: -> :git-genui.user.config.json
    configFilePathPromise: -> Promise.then -> &path.join &os.homedir(), @configBasename

  @getter
    :currentProjectKey
    accounts: -> @config.accounts ? {}
    commitOptions: -> @config.commitOptions ? {}
    commitOptionsForProject: (projectKey = @currentProjectKey) ->
      @commitOptions[projectKey]

  @setter
    accounts: (a) -> @setConfigProperty :accounts, a

  init: ->
    super
    .then -> &git.origin
    .catch ->
    .then (origin) -> @_currentProjectKey = origin

  saveCommitOptionsForProject: (commitOptions, projectKey = @currentProjectKey) ->
    throw new Error "projectKey required to save default options" unless present projectKey
    @setConfigProperty
      :commitOptions
      merge @commitOptions,
        [projectKey]: merge
          commitOptions
          {}
            updatedAt = toSeconds()
            createdAt: @commitOptions[projectKey]?.createdAt ? updatedAt
